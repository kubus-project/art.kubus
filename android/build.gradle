allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.reown.com/releases' }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

subprojects {
    plugins.withId('com.android.application') {
        def androidExt = project.extensions.findByName('android')
        if (androidExt != null && androidExt.hasProperty('compileOptions')) {
            androidExt.compileOptions {
                sourceCompatibility JavaVersion.VERSION_21
                targetCompatibility JavaVersion.VERSION_21
            }
        }
    }
    plugins.withId('com.android.library') {
        def androidExt = project.extensions.findByName('android')
        if (androidExt != null && androidExt.hasProperty('compileOptions')) {
            androidExt.compileOptions {
                sourceCompatibility JavaVersion.VERSION_21
                targetCompatibility JavaVersion.VERSION_21
            }
        }
    }
    plugins.withId('org.jetbrains.kotlin.android') {
        def androidExt = project.extensions.findByName('android')
        if (androidExt != null && androidExt.hasProperty('kotlinOptions')) {
            androidExt.kotlinOptions {
                jvmTarget = '21'
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}